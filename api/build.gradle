apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.cedar.dockerplugin'

repositories {
  maven {
    url "http://packages.confluent.io/maven/"
  }
}


dependencies {
  compile('org.springframework.boot:spring-boot-starter-data-rest')
  compile('org.springframework.cloud:spring-cloud-starter')
  compile('org.apache.kafka:kafka-clients:1.0.0')
  compile "org.apache.avro:avro:1.8.2"
  compile 'io.confluent:kafka-avro-serializer:3.2.1'
  compile('org.codehaus.groovy:groovy')
  compile('org.codehaus.groovy:groovy-json')

  runtime('org.springframework.boot:spring-boot-devtools')

  providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile("org.spockframework:spock-spring:${project.spockVersion}")
  testCompile("org.spockframework:spock-core:${project.spockVersion}")
}

war {
  // build a deployable war in addition to the executable one
  enabled = true
}
configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

bootWar {
  // add a classifier to distinguish the executable war
  classifier = 'all'
  launchScript()
}

docker {
  additionalBuildArgs = [WAR_NAME: bootWar.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(bootWar)
assemble.dependsOn(BuildDockerImage)
PublishDockerImage.dependsOn(BuildDockerImage)
task publish { dependsOn PublishDockerImage }
