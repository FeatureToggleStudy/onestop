buildscript {
  ext {
    spockVersion = '1.1-groovy-2.4'
    schemasVersion = '0.2.0'
  }
  dependencies {
    classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
  }
}

plugins {
  id "org.owasp.dependencycheck" version "4.0.0.1"
}

dependencyCheck {
  skipConfigurations = ["providedRuntime"]
  suppressionFile = "${rootDir}/owasp-suppressions.xml"
  failBuildOnCVSS = 4

  // One of our dependencies has an un-parsable pom which causes dependency-checker
  // to throw an exception. However, the checks still run and it still generates a
  // report, so I think it's safe(ish) to ignore the error.
  failOnError = false
}
task('check').dependsOn('dependencyCheckAggregate')
task('build').dependsOn('check')

group = 'org.cedar.psi'

subprojects {
  group = rootProject.group
  version = rootProject.version

  afterEvaluate {
    repositories {
      mavenCentral()
      maven { url "https://repo.spring.io/milestone" }
      maven { url 'https://packages.confluent.io/maven/' }
      maven { url "https://jitpack.io" }
    }

    if (project.plugins.hasPlugin('org.cedar.dockerplugin')) {
      docker {
        username = System.getenv('DOCKER_USER')
        password = System.getenv('DOCKER_PASSWORD')
      }
    }

    if (project.plugins.hasPlugin('jacoco')) {
      jacocoTestReport {
        executionData fileTree(projectDir).include("build/jacoco/*.exec")

        reports {
          xml.enabled true
          xml.destination = file("${buildDir}/reports/jacoco/report.xml")
          html.enabled true
          html.destination = file("${buildDir}/reports/jacoco/html")
        }
      }
      check.dependsOn jacocoTestReport
    }
    
    // override dependency versions
    configurations.all {
      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.fasterxml.jackson.core' &&
            details.requested.name == 'jackson-databind' &&
            details.requested.version < '2.9.8') {
          details.useVersion '2.9.8'
          details.because 'Enforce jackson-databind 2.9.8+ to avoid vulnerabilities CVE-2018-19360, CVE-2018-19361, CVE-2018-19362, CVE-2018-1000873'
        }
      }

      resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group.startsWith('org.apache.tomcat') &&
            details.requested.name.contains('tomcat') &&
            details.requested.version <= '9.0.17') {
          details.useVersion '9.0.19'
          details.because 'Enforce tomcat 9.0.18+ to avoid vulnerabilities CVE-2019-0199 and CVE-2019-0232'
        }
      }
    }
    
  }

}
