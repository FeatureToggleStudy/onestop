buildscript {
  ext {
    springBootVersion = '2.0.0.RC1'
  }
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.gradle:build-scan-plugin:1.11'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "gradle.plugin.org.cedar:dockerplugin:1.0.2"
    classpath 'org.owasp:dependency-check-gradle:3.1.1'
  }
}

apply plugin: 'org.owasp.dependencycheck'

group = 'org.cedar.psi'

subprojects {
  group = rootProject.group
  version = rootProject.version

  ext {
    spockVersion = '1.1-groovy-2.4'
    springCloudVersion = 'Finchley.M5'
  }

  afterEvaluate {
    repositories {
      mavenCentral()
      maven { url "https://repo.spring.io/milestone" }
    }

    dependencyManagement {
      imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
      }
    }

    configurations {
      compile.exclude module: 'tomcat-annotations-api', group: 'org.apache.tomcat'
    }

    if (project.plugins.hasPlugin('org.cedar.dockerplugin')) {
      docker {
        username = System.getenv('DOCKER_USER')
        password = System.getenv('DOCKER_PASSWORD')
      }
    }
  }
}

task composeStart(type: Exec, description: 'Runs the entire system in docker containers', group: 'docker') {
  dependsOn ":api:build", ":parser:build", ":registry:build", ":script-wrapper:build"
  executable = "bash"
  args = ["-c", "docker-compose up -d"]
}

task composeStop(type: Exec, description: 'Stops the entire system in docker containers', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose stop"]
}

task composeInfraStart(type: Exec, description: 'Runs infrastructure needed under our apps, e.g. elasticsearch', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose up -d elasticsearch kafka kafka-connect kafka-rest kafka-ui schema-registry zookeeper"]
}

task composeInfraStop(type: Exec, description: 'Stops the infrastructure needed under our apps, e.g. elasticsearch', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose stop elasticsearch kafka kafka-connect kafka-rest kafka-ui schema-registry zookeeper"]
}

task composeClean(type: Exec, description: 'Stops and removes the entire system of docker containers', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose down && docker-compose rm"]
}

task clean {
  dependsOn composeClean
}
