plugins {
  id 'groovy'
  id 'application'
  id 'jacoco'
  id 'org.cedar.dockerplugin' version '1.0.3'
  id "com.github.johnrengelman.shadow" version "4.0.3"
}

mainClassName = 'org.cedar.psi.manager.StreamManagerMain'

repositories {
  mavenCentral()
}

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
  compile(project(':common'))
  compile('org.apache.commons:commons-text:1.2')
  compile('org.apache.kafka:kafka-streams:2.1.0')
  compile('org.apache.kafka:kafka-clients:2.1.0')
  compile('io.confluent:kafka-streams-avro-serde:5.1.0')
  compile('org.slf4j:slf4j-api:1.7.25')
  compile('ch.qos.logback:logback-classic:1.2.3')
  
  compile("com.github.cedardevs.schemas:schemas-parse:${project.schemasVersion}")
  compile("com.github.cedardevs.schemas:schemas-analyze:${project.schemasVersion}")

  testCompile("org.spockframework:spock-core:${project.spockVersion}")
  testCompile('org.apache.kafka:kafka-streams-test-utils:2.1.0')
  testCompile('org.apache.kafka:kafka-clients:2.1.0:test')
  testCompile('org.apache.kafka:kafka_2.12:2.1.0')
  testCompile('org.apache.kafka:kafka_2.12:2.1.0:test')
  testCompile('io.confluent:kafka-schema-registry:5.1.0')
  testCompile('io.confluent:kafka-schema-registry:5.1.0:tests')
  testCompile('com.github.java-json-tools:json-schema-validator:2.2.10')
  testCompile("com.github.cedardevs.schemas:schemas-core:${project.schemasVersion}:test")
  testCompile("com.github.cedardevs.schemas:schemas-core:${project.schemasVersion}:sources")

}

jar {
  baseName = "${rootProject.name}-${project.name}"
  manifest {
    attributes 'Main-Class': mainClassName
  }
}
// check http://imperceptiblethoughts.com/shadow/
shadowJar {
  baseName = "${rootProject.name}-${project.name}"
  classifier = 'all'
  version = version
  configurations = [project.configurations.compile]
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  baseName = "${rootProject.name}-${project.name}"
  from "${projectDir}/src"
}

docker {
  additionalBuildArgs = [JAR_NAME: shadowJar.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(shadowJar)
assemble.dependsOn(BuildDockerImage)
assemble.dependsOn(sourceJar)
PublishDockerImage.dependsOn(BuildDockerImage)
task publish { dependsOn PublishDockerImage }
