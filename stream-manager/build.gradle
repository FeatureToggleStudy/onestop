plugins {
  id 'groovy'
  id 'application'
  id 'jacoco'
  id 'org.cedar.dockerplugin' version '1.0.3'
  id "com.google.cloud.tools.jib" version "1.6.1"
  id "com.github.johnrengelman.shadow" version "4.0.3"
}

mainClassName = 'org.cedar.psi.manager.StreamManagerMain'

repositories {
  mavenCentral()
}

sourceCompatibility = '11'

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
  compile(project(':common'))
  compile("org.apache.commons:commons-text:1.2")
  compile("org.apache.kafka:kafka-streams:${project.kafkaVersion}")
  compile("org.apache.kafka:kafka-clients:${project.kafkaVersion}")
  compile("io.confluent:kafka-streams-avro-serde:${project.confluentVersion}")
  compile("org.slf4j:slf4j-api:1.7.25")
  compile("ch.qos.logback:logback-classic:1.2.3")
  compile('org.yaml:snakeyaml:1.24')
  compile 'com.google.guava:guava:27.0.1-jre'

  compile("com.github.cedardevs.schemas:schemas-parse:${project.schemasVersion}")
  compile("com.github.cedardevs.schemas:schemas-analyze:${project.schemasVersion}")

  testCompile("org.spockframework:spock-core:${project.spockVersion}")
  testCompile("com.github.stefanbirkner:system-rules:1.19.0")
  testCompile("org.apache.kafka:kafka-streams-test-utils:${project.kafkaVersion}")
  testCompile("org.apache.kafka:kafka-clients:${project.kafkaVersion}:test")
  testCompile("org.apache.kafka:kafka_2.12:${project.kafkaVersion}")
  testCompile("org.apache.kafka:kafka_2.12:${project.kafkaVersion}:test")
  testCompile("io.confluent:kafka-schema-registry:${project.confluentVersion}")
  testCompile("io.confluent:kafka-schema-registry:${project.confluentVersion}:tests")
  testCompile("com.github.java-json-tools:json-schema-validator:2.2.10")
  testCompile("com.github.cedardevs.schemas:schemas-core:${project.schemasVersion}:test")
  testCompile("com.github.cedardevs.schemas:schemas-core:${project.schemasVersion}:sources")

}

jar {
  baseName = "${rootProject.name}-${project.name}"
  manifest {
    attributes 'Main-Class': mainClassName
  }
}
// check http://imperceptiblethoughts.com/shadow/
shadowJar {
  baseName = "${rootProject.name}-${project.name}"
  classifier = 'all'
  version = version
  configurations = [project.configurations.compile]
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  baseName = "${rootProject.name}-${project.name}"
  from "${projectDir}/src"
}

docker {
  additionalBuildArgs = [JAR_NAME: shadowJar.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(shadowJar)
assemble.dependsOn(BuildDockerImage)
assemble.dependsOn(sourceJar)
PublishDockerImage.dependsOn(BuildDockerImage)
task publish { dependsOn PublishDockerImage }

jib {
  extraDirectories {
    paths = ["${buildDir}/jib-dir".toString()]
  }
  // derive image repository
  String imageRegistryURL = "registry.hub.docker.com"
  String imageNamespace = "cedardevs"
  String imageName = "${rootProject.name}-${project.name}".toString()
  // TODO: remove the `-jib` post-fix once we are sure everything is working smoothly with JIB
  // this prevents us from clobbering things we already published to Docker Hub
  String imageTag = "${project.version}-jib".toString()
  String imageRepository = "${imageRegistryURL}/${imageNamespace}/${imageName}:${imageTag}".toString()

  from {
    //base image
    image = 'gcr.io/distroless/java:11'
  }
  to {
    image = imageRepository
    auth {
      username = System.getenv('DOCKER_USER').toString().trim()
      password = System.getenv('DOCKER_PASSWORD').toString().trim()
    }
  }
  container {
    // http://label-schema.org/rc1/
    labels = [
        'org.label-schema.build-date': project.buildDate.toString(),
        'org.label-schema.name': imageName,
        'org.label-schema.description': 'A search API for the OneStop search software.',
        'org.label-schema.vcs-url': project.getVcsUrl().toString(),
        'org.label-schema.vcs-ref': project.getVcsRef().toString(),
        'org.label-schema.vendor': imageNamespace,
        'org.label-schema.version': imageTag,
        'org.label-schema.schema-version': '1.0',
    ]
    ports = ['80'.toString()]
  }
}