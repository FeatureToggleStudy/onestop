// Defines a 'deploy' task which deploys the client and api via ansible
// Assumes that ansible is installed and that an inventory file named 'hosts'
// is present in the ansible subproject dir (next to this build script)

task deploy {
    dependsOn ':api:bootRepackage', ':client:assemble'

    doLast {
        def apiOutput = project(':api').outputJar
        def clientOutput = project(':client').tasks.findByName('assemble').outputs.files.singleFile

        println clientOutput

        def proc = new ProcessBuilder()
          .command(
            'ansible-playbook',
            'deploy-onestop.yml',
            '--inventory', './hosts',
            '--user', 'root',
            '--extra-vars', "apiJar=${apiOutput} clientTar=${clientOutput}"
          )
          .directory(projectDir)
          .start()

        proc.waitForProcessOutput(System.out, System.err)
        if (proc.exitValue() != 0) {
            throw new RuntimeException("Ansible playbook failed")
        }
    }
}
