import groovy.json.JsonSlurper
import com.moowork.gradle.node.task.NodeTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:0.13'
    }
}

apply plugin: "com.moowork.node"

node {
    version = '6.3.1'
    download = true
}

version = new JsonSlurper().parse(file("${projectDir}/package.json")).version

task clean {
    doFirst {
        file("${projectDir}/dist").deleteDir()
        file("${buildDir}/libs").deleteDir()
    }
}

def sourceDirs = ['node_modules', 'img', 'src', 'style']

task compile(type: NodeTask) {
    dependsOn 'npmInstall'
    sourceDirs.each {
        inputs.sourceDir(it)
    }
    outputs.dir('dist')
    script = file('node_modules/.bin/webpack')
    execOverrides {
        it.environment 'NODE_ENV', 'production'
    }
    doFirst {
        // Remove previous outputs. Doesn't run if the task if UP-TO-DATE
        file("${projectDir}/dist").deleteDir()
    }
}

task assemble(type: Tar) {
    dependsOn 'compile'

    from "${projectDir}/dist"
    baseName = 'onestop-client'
    version = project.version
    extension = 'tar.gz'
    destinationDir = file("${buildDir}/libs")
}

task npm_test {
    dependsOn 'compile'
    sourceDirs.each {
        inputs.sourceDir(it)
    }
    inputs.sourceDir('test')
}

task test {
    dependsOn 'npm_test'
}

task build {
    dependsOn 'assemble', 'test'
}